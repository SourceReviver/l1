# The `l1` Language

[Jump to API](#api-index) / list of operators

## Expressions

Expressions in `l1` are atoms, lists, numbers, or functions.

Atoms have a name, such as `x`, `foo`, or `Eisenhower!`, and can be
"bound to an expression in the current environment," meaning,
essentially, given a value. For example, we can bind a value to `a`
and retrieve it:

    > (def a 1)
    > a
    1
    >

Lists are collections of zero or more expressions.  Examples:

    (getting atomic)
    (10 9 8 7 6 5 4 3 2 1)
    (+ 2 2)
    ()

In general, lists represent operations whose name is the first element
and whose arguments are the remaining elements.  For example, `(+ 2
2)` is a list that evaluates to the number `4`.  To prevent evaluation
of a list, prepend a quote character:

    > '(+ 2 2)
    (+ 2 2)
    > (+ 2 2)
    4

`()` is alternatively called the empty list, or nil.

Numbers are integer values and can be of arbitrary magnitude:

    0
    999
    7891349058731409803589073418970341089734958701432789

Numbers evaluate to themselves:

    > 0
    0
    > 7891349058731409803589073418970341089734958701432789
    7891349058731409803589073418970341089734958701432789

Numbers and atoms can be turned into lists:

    > (split 'atomic)
    (a t o m i c)
    > (split 1234)
    (1 2 3 4)

And lists can be turned into atoms or numbers:

    > (fuse '(getting atomic))
    gettingatomic
    > (fuse '(10 9 8 7 6 5 4 3 2 1))
    10987654321

## Boolean Logic

In `l1`, `()` is the only "falsey" value; everything else is "truthy".
Falsey and truthy are important when evaluating conditional statements
such as `if`, `when`, or `cond`.  The default truthy value is `t`.
`t` and `()` evaluate to themselves.

The `and`, `or` and `not` operators work like they do in most other
languages:

    > (and t t)
    t
    > (or () ())
    ()
    > (or () 135987)
    135987
    > (and () (launch missiles))
    ()
    > (not t)
    ()
    > (not ())
    t
    > (not 13987)
    ()
    > (if ()
        (launch missiles)
        555)
    555

## Special Characters

Unlike many modern languages, `l1` doesn't have strings.  Instead,
atoms and lists are used where strings normally would be:

    > (printl '(Hello, world!))
    Hello, world!
    ()

(In this example, the `Hello, world!` is output to the terminal, and
then the return value of `printl`, namely `()`.)

Some characters, like `!`, need special handling, since they are "unreadable":

    > (printl '(!Hello, world!))
    unexpected lexeme 'unexpected character '!' in input'
    > (printl `(~(fuse `(~BANG Hello,)) world!))
    !Hello, world!
    ()

This is admittedly awkward, but rare in practice for the kinds of
programs `l1` was designed for.  `BANG` is one of a small set of atoms
helpful for this sort of construction:

    BANG
    COLON
    COMMA
    NEWLINE
    PERIOD
    QMARK
    SPACE
    TAB

These all evaluate to atoms whose names are the unreadable characters,
some of which may be helpful for text games and other diversions:

    > (dotimes 10
        (println
         (fuse
          (repeatedly 10
                      (lambda ()
                        (randchoice (list COMMA
                                          COLON
                                          PERIOD
                                          BANG
                                          QMARK)))))))

    .!!!.???..
    ,??::?!,.?
    ?,?!?..:!!
    ,:.,?.:!!!
    !!:?!::.,?
    ,:!!!:,!!:
    ,???:?!:!?
    .,!!?,!:!?
    !:,!!!.:!:
    ??.,,:.:..
    ()
    >

## Functions

Functions come in two flavors: temporary functions, called "lambda"
functions for historical reasons, and functions which are defined and
kept around in the environment for later use.  For example,

    > (defn plus2 (x) (+ x 2))
    > (plus2 3)
    5
    > ((lambda (x) (* 5 x)) 3)
    15

Functions can take a fixed number of arguments plus an extra "rest"
argument, separated from the fixed arguments with a "."; the rest
argument is then bound to a list of all remaining arguments:

    > (defn multiply-then-sum (multiplier . xs)
        (* multiplier (apply + xs)))
    ()
    > (multiply-then-sum 5 1)
    5
    > (multiply-then-sum 5 1 2 3)
    30

A function that has a rest argument but no fixed arguments is
specified using the empty list as its fixed argument:

    > (defn say-hello (() . friends)
        (list* 'hello friends))
    > (say-hello 'John 'Jerry 'Eden)
    (hello John Jerry Eden)

Functions may invoke themselves recursively:

    > (defn sum-nums (l)
        (if-not l
          0
          (+ (car l) (sum-nums (cdr l)))))
    ()
    > (sum-nums '(0 1 2 3 4 5 6 7 8 9))
    45

The above function performs an addition after it invokes itself.  A
function which invokes itself *immediately before returning*, without
doing any more work, is called "tail recursive."  Such functions are
turned into iterations automatically by the interpreter ("tail call
optimization").  The above function can be rewritten into a
tail-recursive version:

    > (defn sum-nums-accumulator (l acc)
        (if-not l
          acc
          (sum-nums-accumulator (cdr l) (+ acc (car l)))))
    ()
    > (sum-nums-accumulator '(0 1 2 3 4 5 6 7 8 9) 0)
    45

Lambda functions can invoke themselves if given a name directly before
the parameters are declared.  We can rewrite the above function to
hide the `acc` argument from the user:

    > (defn sum-nums (l)
        (let ((inner (lambda inner (l acc)
                       (if-not l
                         acc
                         (inner (cdr l) (+ acc (car l)))))))
          (inner l 0)))
    ()
    > (sum-nums '(0 1 2 3 4 5 6 7 8 9))
    45

This version is both tail-recursive (in `inner`), and as convenient to
use as our first, non-tail-recursive version was.

## Text User Interfaces

`l1` has a few built-in functions for creating simple text UIs:

- `with-screen`: Enter/exit "screen" (UI) mode
- `screen-clear`: Clear the screen
- `screen-get-key`: Get a keystroke
- `screen-write`: Write a list, without parentheses, to an `x` and `y` position on the screen.

[An example
program](https://github.com/eigenhombre/l1/blob/master/examples/screen-test.l1)
shows these functions in action.

## Emacs Integration

If you are using Emacs, you can set it up to work with `l1` as an "inferior
lisp" process as described in [the Emacs manual](https://www.gnu.org/software/emacs/manual/html_node/emacs/External-Lisp.html).
I currently derive a new major mode from the base `lisp-mode` and bind a few
keys for convenience as follows:

    (define-derived-mode l1-mode
      lisp-mode "L1 Mode"
      "Major mode for L1 Lisp code"
      (setq inferior-lisp-program (executable-find "l1")
      (paredit-mode 1)
      (put 'test 'lisp-indent-function 1)
      (put 'testing 'lisp-indent-function 1)
      (put 'errors 'lisp-indent-function 1)
      (put 'if 'lisp-indent-function 1)
      (put 'if-not 'lisp-indent-function 1)
      (put 'foreach 'lisp-indent-function 2)
      (put 'when-not 'lisp-indent-function 1)
      (define-key l1-mode-map (kbd "s-i") 'lisp-eval-last-sexp)
      (define-key l1-mode-map (kbd "s-I") 'lisp-eval-form-and-next)
      (define-key l1-mode-map (kbd "C-o j") 'run-lisp))

    (add-to-list 'auto-mode-alist '("\\.l1" . l1-mode))

If `l1` has been installed on your path, `M-x run-lisp` or using the appropriate
keybinding should be enough to start a REPL within Emacs and start sending
expressions to it.
# API Index
109 forms available:
[`*`](#*)
[`**`](#**)
[`+`](#+)
[`-`](#-)
[`/`](#/)
[`<`](#<)
[`<=`](#<=)
[`=`](#=)
[`>`](#>)
[`>=`](#>=)
[`and`](#and)
[`apply`](#apply)
[`atom?`](#atom?)
[`bang`](#bang)
[`body`](#body)
[`butlast`](#butlast)
[`caar`](#caar)
[`capitalize`](#capitalize)
[`car`](#car)
[`cdr`](#cdr)
[`colon`](#colon)
[`comma`](#comma)
[`comment`](#comment)
[`complement`](#complement)
[`concat`](#concat)
[`cond`](#cond)
[`cons`](#cons)
[`constantly`](#constantly)
[`dec`](#dec)
[`def`](#def)
[`defmacro`](#defmacro)
[`defn`](#defn)
[`doc`](#doc)
[`dotimes`](#dotimes)
[`downcase`](#downcase)
[`drop`](#drop)
[`error`](#error)
[`errors`](#errors)
[`even?`](#even?)
[`exclaimed`](#exclaimed)
[`filter`](#filter)
[`flatten`](#flatten)
[`foreach`](#foreach)
[`forms`](#forms)
[`fuse`](#fuse)
[`help`](#help)
[`identity`](#identity)
[`if`](#if)
[`if-not`](#if-not)
[`inc`](#inc)
[`is`](#is)
[`lambda`](#lambda)
[`last`](#last)
[`len`](#len)
[`let`](#let)
[`list`](#list)
[`list*`](#list*)
[`list?`](#list?)
[`loop`](#loop)
[`macroexpand-1`](#macroexpand-1)
[`map`](#map)
[`mapcat`](#mapcat)
[`neg?`](#neg?)
[`not`](#not)
[`nth`](#nth)
[`number?`](#number?)
[`odd?`](#odd?)
[`or`](#or)
[`period`](#period)
[`pos?`](#pos?)
[`print`](#print)
[`printl`](#printl)
[`println`](#println)
[`progn`](#progn)
[`punctuate`](#punctuate)
[`punctuate-atom`](#punctuate-atom)
[`quote`](#quote)
[`randalpha`](#randalpha)
[`randchoice`](#randchoice)
[`randigits`](#randigits)
[`randint`](#randint)
[`range`](#range)
[`readlist`](#readlist)
[`reduce`](#reduce)
[`rem`](#rem)
[`remove`](#remove)
[`repeat`](#repeat)
[`repeatedly`](#repeatedly)
[`reverse`](#reverse)
[`screen-clear`](#screen-clear)
[`screen-end`](#screen-end)
[`screen-get-key`](#screen-get-key)
[`screen-size`](#screen-size)
[`screen-start`](#screen-start)
[`screen-write`](#screen-write)
[`shuffle`](#shuffle)
[`sleep`](#sleep)
[`some`](#some)
[`split`](#split)
[`syntax-quote`](#syntax-quote)
[`take`](#take)
[`test`](#test)
[`tosentence`](#tosentence)
[`upcase`](#upcase)
[`version`](#version)
[`when`](#when)
[`when-not`](#when-not)
[`with-screen`](#with-screen)
[`zero?`](#zero?)
# Operators


## `*`

Multiply 0 or more numbers

Type: native function

Arity: 0+

Args: `(() . xs)`


### Examples

```
> (* 1 2 3)
;;=>
6
> (*)
;;=>
1

```

-----------------------------------------------------
		

## `**`

Exponentiation operator

Type: function

Arity: 2 

Args: `(n m)`


### Examples

```
> (** 1 0)
;;=>
1
> (** 2 4)
;;=>
16
> (** 10 10)
;;=>
10000000000

```

-----------------------------------------------------
		

## `+`

Add 0 or more numbers

Type: native function

Arity: 0+

Args: `(() . xs)`


### Examples

```
> (+ 1 2 3)
;;=>
6
> (+)
;;=>
0

```

-----------------------------------------------------
		

## `-`

Subtract 0 or more numbers from the first argument

Type: native function

Arity: 1+

Args: `(x . xs)`


### Examples

```
> (- 1 1)
;;=>
0
> (- 5 2 1)
;;=>
2
> (- 99)
;;=>
-99

```

-----------------------------------------------------
		

## `/`

Divide the first argument by the rest

Type: native function

Arity: 2+

Args: `(numerator denominator1 . more)`


### Examples

```
> (/ 1 2)
;;=>
0
> (/ 12 2 3)
;;=>
2
> (/ 1 0)
;;=>
ERROR: division by zero

```

-----------------------------------------------------
		

## `<`

Return t if the arguments are in strictly increasing order, () otherwise

Type: native function

Arity: 1+

Args: `(x . xs)`


### Examples

```
> (< 1 2)
;;=>
t
> (< 1 1)
;;=>
()
> (< 1)
;;=>
t
> (apply < (range 100))
;;=>
t

```

-----------------------------------------------------
		

## `<=`

Return t if the arguments are in increasing (or qual) order, () otherwise

Type: native function

Arity: 1+

Args: `(x . xs)`


### Examples

```
> (<= 1 2)
;;=>
t
> (<= 1 1)
;;=>
t
> (<= 1)
;;=>
t

```

-----------------------------------------------------
		

## `=`

Return t if the arguments are equal, () otherwise

Type: native function

Arity: 1+

Args: `(x . xs)`


### Examples

```
> (= 1 1)
;;=>
t
> (= 1 2)
;;=>
()
> (apply = (repeat 10 t))
;;=>
t

```

-----------------------------------------------------
		

## `>`

Return t if the arguments are in strictly decreasing order, () otherwise

Type: native function

Arity: 1+

Args: `(x . xs)`


### Examples

```
> (> 1 2)
;;=>
()
> (> 1 1)
;;=>
()
> (> 1)
;;=>
t

```

-----------------------------------------------------
		

## `>=`

Return t if the arguments are in decreasing (or equal) order, () otherwise

Type: native function

Arity: 1+

Args: `(x . xs)`


### Examples

```
> (>= 1 2)
;;=>
()
> (>= 1 1)
;;=>
t

```

-----------------------------------------------------
		

## `and`

Boolean and

Type: special form

Arity: 0+

Args: `(() . xs)`


### Examples

```
(and)
;;=>
true
> (and t t)
;;=>
true
> (and t t ())
;;=>
()
> (and () (/ 1 0))
;;=>
()

```

-----------------------------------------------------
		

## `apply`

Apply a function to a list of arguments

Type: native function

Arity: 2 

Args: `(f args)`


### Examples

```
> (apply + (repeat 10 1))
;;=>
10
> (apply * (cdr (range 10)))
;;=>
362880

```

-----------------------------------------------------
		

## `atom?`

Return t if the argument is an atom, () otherwise

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (atom? 1)
;;=>
()
> (atom? (quote one))
;;=>
t

```

-----------------------------------------------------
		

## `bang`

Add an exclamation point at end of atom

Type: function

Arity: 1 

Args: `(a)`


### Examples

```
> (bang (quote Bang))
;;=>
Bang!

```

-----------------------------------------------------
		

## `body`

Return the body of a lambda function

Type: native function

Arity: 1 

Args: `(f)`


### Examples

```
> (body (lambda (x) (+ x 1)))
;;=>
((+ x 1))

```

-----------------------------------------------------
		

## `butlast`

Return everything but the last element

Type: function

Arity: 1 

Args: `(l)`


### Examples

```
> (butlast ())
;;=>
()
> (butlast (range 3))
;;=>
(0 1)

```

-----------------------------------------------------
		

## `caar`

First element of the first element of a list of lists

Type: function

Arity: 1 

Args: `(l)`


### Examples

```
> (caar ())
;;=>
()
> (caar (quote (())))
;;=>
()
> (caar (quote ((one two) (three four))))
;;=>
one

```

-----------------------------------------------------
		

## `capitalize`

Return the atom argument, capitalized

Type: function

Arity: 1 

Args: `(a)`


### Examples

```
> (capitalize (quote hello))
;;=>
Hello

```

-----------------------------------------------------
		

## `car`

Return the first element of a list

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (car (quote (one two)))
;;=>
one
> (car ())
;;=>
()

```

-----------------------------------------------------
		

## `cdr`

Return a list with the first element removed

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (cdr (quote (one two)))
;;=>
(two)
> (cdr ())
;;=>
()

```

-----------------------------------------------------
		

## `colon`

Add a colon at end of atom

Type: function

Arity: 1 

Args: `(a)`


### Examples

```
> (colon (quote remember-this))
;;=>
remember-this:

```

-----------------------------------------------------
		

## `comma`

Add a comma at end of atom

Type: function

Arity: 1 

Args: `(a)`


### Examples

```
> (comma (quote hello))
;;=>
hello,

```

-----------------------------------------------------
		

## `comment`

Ignore the expressions in the block

Type: macro

Arity: 0+

Args: `(() . body)`


### Examples

```
> (comment twas brillig, and the slithy toves did gyre and gimble in the wabe)
;;=>
()

```

-----------------------------------------------------
		

## `complement`

Return the logical complement of the supplied function

Type: function

Arity: 1 

Args: `(f)`


### Examples

```
> ((complement even?) 1)
;;=>
t
> (map (complement odd?) (range 5))
;;=>
(t () t () t)

```

-----------------------------------------------------
		

## `concat`

Concatenenate any number of lists

Type: function

Arity: 0+

Args: `(() . lists)`


-----------------------------------------------------
		

## `cond`

Fundamental branching construct

Type: special form

Arity: 0+

Args: `(() . pairs)`


### Examples

```
> (cond)
;; => ()
> (cond (t 1) (t 2) (t 3))
;; => 1
> (cond (() 1) (t 2))
;; => 2

```

-----------------------------------------------------
		

## `cons`

Add an element to the front of a (possibly empty) list

Type: native function

Arity: 2 

Args: `(x xs)`


### Examples

```
> (cons 1 (quote (one two)))
;;=>
(1 one two)
> (cons 1 ())
;;=>
(1)
> (cons 1 2)
;;=>
(1 . 2)

```

-----------------------------------------------------
		

## `constantly`

Given a value, return a function which always returns that value

Type: function

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `dec`

Return the supplied integer argument, minus one

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `def`

Set a value

Type: special form

Arity: 2 

Args: `(name value)`


-----------------------------------------------------
		

## `defmacro`

Create and name a macro

Type: special form

Arity: 2+

Args: `(name args . body)`


-----------------------------------------------------
		

## `defn`

Create and name a function

Type: special form

Arity: 2+

Args: `(name args . body)`


-----------------------------------------------------
		

## `doc`

Return the doclist for a function

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (doc (lambda (x) (doc (does stuff)) (+ x 1)))
;;=>
((does stuff))

```

-----------------------------------------------------
		

## `dotimes`

Execute body for each value in a list

Type: macro

Arity: 1+

Args: `(n . body)`


-----------------------------------------------------
		

## `downcase`

Return a new atom with all characters in lower case

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (downcase (quote Hello))
;;=>
hello
> (downcase (quote HELLO))
;;=>
hello

```

-----------------------------------------------------
		

## `drop`

Drop n items from a list, then return the rest

Type: function

Arity: 2 

Args: `(n l)`


-----------------------------------------------------
		

## `error`

Raise an error

Type: special form

Arity: 1 

Args: `(msg-list)`


-----------------------------------------------------
		

## `errors`

Error checking (for tests)

Type: special form

Arity: 1+

Args: `(message-pattern-list . exprs)`


-----------------------------------------------------
		

## `even?`

Return true if the supplied integer argument is even

Type: function

Arity: 1 

Args: `(n)`


### Examples

```
> (map even? (range 5))
;;=>
(t () t () t)

```

-----------------------------------------------------
		

## `exclaimed`

Return l as a sentence... emphasized!

Type: function

Arity: 1 

Args: `(l)`


-----------------------------------------------------
		

## `filter`

Keep only values for which function f is true

Type: function

Arity: 2 

Args: `(f l)`


-----------------------------------------------------
		

## `flatten`

Return a (possibly nested) list, flattened

Type: function

Arity: 1 

Args: `(l)`


### Examples

```
> (flatten (quote (this is a (really (nested) list))))
;;=>
(this is a really nested list)

```

-----------------------------------------------------
		

## `foreach`

Execute body for each value in a list

Type: macro

Arity: 2+

Args: `(x xs . body)`


-----------------------------------------------------
		

## `forms`

Return available operators, as a list

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `fuse`

Fuse a list of numbers or atoms into a single atom

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (fuse (quote (A B C)))
;;=>
ABC
> (fuse (reverse (range 10)))
;;=>
9876543210

```

-----------------------------------------------------
		

## `help`

Print this message

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `identity`

Return the argument

Type: function

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `if`

Simple conditional with two branches

Type: macro

Arity: 3 

Args: `(condition then else)`


-----------------------------------------------------
		

## `if-not`

Simple (inverted) conditional with two branches

Type: macro

Arity: 3 

Args: `(condition then else)`


-----------------------------------------------------
		

## `inc`

Return the supplied integer argument, plus one

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `is`

Assert a condition is truthy, or show failing code

Type: macro

Arity: 1 

Args: `(expr)`


### Examples

```
> (is t)
;;=>
()
> (is (car (cons () (quote (this one should fail)))))
;;=>
ERROR: (assertion failed: (car (cons () (quote (this one should fail)))))

```

-----------------------------------------------------
		

## `lambda`

Create a function

Type: special form

Arity: 1+

Args: `(args . body) or (name args . body)`


-----------------------------------------------------
		

## `last`

Return the last item in a list

Type: function

Arity: 1 

Args: `(l)`


-----------------------------------------------------
		

## `len`

Return the length of a list

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (len (range 10))
;;=>
10

```

-----------------------------------------------------
		

## `let`

Create a local scope with bindings

Type: special form

Arity: 1+

Args: `(bindings . body)`


-----------------------------------------------------
		

## `list`

Return a list of the given arguments

Type: native function

Arity: 0+

Args: `(() . xs)`


### Examples

```
> (list 1 2 3)
;;=>
(1 2 3)
> (list)
;;=>
()

```

-----------------------------------------------------
		

## `list*`

Create a list by consing everything but the last arg onto the last

Type: function

Arity: 0+

Args: `(() . args)`


-----------------------------------------------------
		

## `list?`

Return t if the argument is a list, () otherwise

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (list? (range 10))
;;=>
t
> (list? 1)
;;=>
()

```

-----------------------------------------------------
		

## `loop`

Loop forever

Type: special form

Arity: 1+

Args: `(() . body)`


-----------------------------------------------------
		

## `macroexpand-1`

Expand a macro

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (macroexpand-1 (quote (+ x 1)))
;;=>
(+ x 1)
> (macroexpand-1 (quote (if () 1 2)))
;;=>
(cond (() 1) (t 2))

```

-----------------------------------------------------
		

## `map`

Apply the supplied function to every element in the supplied list

Type: function

Arity: 2 

Args: `(f l)`


-----------------------------------------------------
		

## `mapcat`

Map a function onto a list and concatenate results

Type: function

Arity: 2 

Args: `(f l)`


-----------------------------------------------------
		

## `neg?`

Return true iff the supplied integer argument is less than zero

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `not`

Return t if the argument is nil, () otherwise

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (not ())
;;=>
t
> (not t)
;;=>
()
> (not (range 10))
;;=>
()

```

-----------------------------------------------------
		

## `nth`

Find the nth value of a list, starting from zero

Type: function

Arity: 2 

Args: `(n l)`


-----------------------------------------------------
		

## `number?`

Return true if the argument is a number, else ()

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (number? 1)
;;=>
t
> (number? t)
;;=>
()
> (number? +)
;;=>
()

```

-----------------------------------------------------
		

## `odd?`

Return true if the supplied integer argument is odd

Type: function

Arity: 1 

Args: `(n)`


### Examples

```
> (map even? (range 5))
;;=>
(t () t () t)

```

-----------------------------------------------------
		

## `or`

Boolean or

Type: special form

Arity: 0+

Args: `(() . xs)`


### Examples

```
> (or)
;; => false
> (or t t)
;; => true
> (or t t ())
;; => t
```

-----------------------------------------------------
		

## `period`

Add a period at end of atom

Type: function

Arity: 1 

Args: `(a)`


### Examples

```
> (period (quote Woot))
;;=>
Woot.

```

-----------------------------------------------------
		

## `pos?`

Return true iff the supplied integer argument is greater than zero

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `print`

Print the arguments

Type: native function

Arity: 0+

Args: `(() . xs)`


-----------------------------------------------------
		

## `printl`

Print a list argument, without parentheses

Type: native function

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `println`

Print the arguments and a newline

Type: native function

Arity: 0+

Args: `(() . xs)`


-----------------------------------------------------
		

## `progn`

Execute multiple statements, returning the last

Type: macro

Arity: 0+

Args: `(() . body)`


-----------------------------------------------------
		

## `punctuate`

Return x capitalized, with punctuation determined by the supplied function

Type: function

Arity: 2 

Args: `(f x)`


-----------------------------------------------------
		

## `punctuate-atom`

Add a punctuation mark at end of atom

Type: function

Arity: 2 

Args: `(a mark)`


-----------------------------------------------------
		

## `quote`

Quote an expression

Type: special form

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `randalpha`

Return a list of random (English/Latin/unaccented) alphabetic characters

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `randchoice`

Return an element at random from the supplied list

Type: function

Arity: 1 

Args: `(l)`


-----------------------------------------------------
		

## `randigits`

Return a random integer between 0 and the argument minus 1

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `randint`

Return a random integer between 0 and the argument minus 1

Type: native function

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `range`

List of integers from 0 to n

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		

## `readlist`

Read a list from stdin

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `reduce`

Successively apply a function against a list of arguments

Type: function

Arity: 2+

Args: `(f x . args)`


-----------------------------------------------------
		

## `rem`

Return remainder when second arg divides first

Type: native function

Arity: 2 

Args: `(x y)`


-----------------------------------------------------
		

## `remove`

Keep only values for which function f is false / the empty list

Type: function

Arity: 2 

Args: `(f l)`


-----------------------------------------------------
		

## `repeat`

Return a list of length n whose elements are all x

Type: function

Arity: 2 

Args: `(n x)`


-----------------------------------------------------
		

## `repeatedly`

Return a list of length n whose elements are made from calling f repeatedly

Type: function

Arity: 2 

Args: `(n f)`


-----------------------------------------------------
		

## `reverse`

Reverse a list

Type: function

Arity: 1 

Args: `(l)`


-----------------------------------------------------
		

## `screen-clear`

Clear the screen

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `screen-end`

Stop screen for text UIs, return to console mode

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `screen-get-key`

Return a keystroke as an atom

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `screen-size`

Return the screen size (width, height)

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `screen-start`

Start screen for text UIs

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `screen-write`

Write a string to the screen

Type: native function

Arity: 3 

Args: `(x y list)`


-----------------------------------------------------
		

## `shuffle`

Return a (quickly!) shuffled list

Type: native function

Arity: 1 

Args: `(xs)`


-----------------------------------------------------
		

## `sleep`

Sleep for the given number of milliseconds

Type: native function

Arity: 1 

Args: `(ms)`


-----------------------------------------------------
		

## `some`

Return f applied to first element for which that result is truthy, else ()

Type: function

Arity: 2 

Args: `(f l)`


-----------------------------------------------------
		

## `split`

Split an atom or number into a list of single-digit numbers or single-character atoms

Type: native function

Arity: 1 

Args: `(x)`


### Examples

```
> (split 123)
;;=>
(1 2 3)
> (split (quote abc))
;;=>
(a b c)

```

-----------------------------------------------------
		

## `syntax-quote`

Syntax-quote an expression

Type: special form

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `take`

Take up to n items from the supplied list

Type: function

Arity: 2 

Args: `(n l)`


-----------------------------------------------------
		

## `test`

Establish a testing block (return last expression)

Type: native function

Arity: 0+

Args: `(() . exprs)`


-----------------------------------------------------
		

## `tosentence`

Return l as a sentence... capitalized, with a period at the end

Type: function

Arity: 1 

Args: `(l)`


-----------------------------------------------------
		

## `upcase`

Return the uppercase version of the given atom

Type: native function

Arity: 1 

Args: `(x)`


-----------------------------------------------------
		

## `version`

Return the version of the interpreter

Type: native function

Arity: 0 

Args: `()`


-----------------------------------------------------
		

## `when`

Simple conditional with single branch

Type: macro

Arity: 2 

Args: `(condition then)`


-----------------------------------------------------
		

## `when-not`

Complement of the when macro

Type: macro

Arity: 2 

Args: `(condition then)`


-----------------------------------------------------
		

## `with-screen`

Prepare for and clean up after screen operations

Type: macro

Arity: 0+

Args: `(() . body)`


-----------------------------------------------------
		

## `zero?`

Return true iff the supplied argument is zero

Type: function

Arity: 1 

Args: `(n)`


-----------------------------------------------------
		
